import org.w3c.dom.Element

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

repositories {
    mavenLocal()
    maven {
        name 'LDTTeam - Modding'
        url 'https://ldtteam.jfrog.io/ldtteam/modding/'
    }
    maven {
        url "https://maven.tehnut.info"
    }
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        url "https://harleyoconnor.com/maven"
    }
    flatDir {
        dirs("libs")
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

ext.configFile = file "gradle.properties"
configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

version = "${config.mc_version}-${config.mod_version}"
group = config.package_group
archivesBaseName = config.mod_name

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: config.mc_version

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            properties 'mixin.env.disableRefMap' : 'true'

            mods {
                dtbop {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            properties 'mixin.env.disableRefMap' : 'true'

            mods {
                dtbop {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            args '--mod', 'dtbop', '--all', '--output', file('src/generated/resources/')

            mods {
                dtbop {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    // Not sure if we need this one, what is a "forge" anyway?
    minecraft "net.minecraftforge:forge:${config.mc_version}-${config.forge_version}"

    // Compile Hwyla API, but don't include in runtime.
//    compileOnly fg.deobf("mcp.mobius.waila:Hwyla:${config.hwyla_version}:api")
    // At runtime, use the full Hwyla mod.
//    runtimeOnly fg.deobf("mcp.mobius.waila:Hwyla:${config.hwyla_version}")

    // Compile JEI API, but don't include in runtime.
    compileOnly fg.deobf("mezz.jei:jei-1.18.2:${config.jei_version}:api")
    // At runtime, use the full JEI mod.
    runtimeOnly fg.deobf("mezz.jei:jei-1.18.2:${config.jei_version}")

    // Compile Dynamic Trees, of course.
    implementation fg.deobf("com.ferreusveritas.dynamictrees:DynamicTrees-${config.mc_version}:${config.dynamic_trees_version}")
    //implementation fg.deobf("blank:DynamicTrees-1.16.5:0.10.0-Beta17")
    // Compile Dynamic Trees Plus
//    runtimeOnly fg.deobf("com.ferreusveritas.dynamictreesplus:DynamicTreesPlus-${config.mc_version}:${config.dynamic_trees_plus_version}")
    implementation fg.deobf("curse.maven:dynamictrees-252818:3815249")

    implementation fg.deobf("curse.maven:biomes-o-plenty-220318:3759236")
    runtimeOnly(fg.deobf("curse.maven:terrablender-563928:3816497"))

    runtimeOnly fg.deobf("curse.maven:cc-tweaked-282001:3825460")

    // At runtime, use suggestion provider fix mod.
//    runtimeOnly fg.deobf("com.harleyoconnor.suggestionproviderfix:SuggestionProviderFix:${config.mc_version}-${config.suggestion_provider_fix_version}")
    runtimeOnly(fg.deobf("curse.maven:suggestionproviderfix-469647:3623382"))

}

jar {
    manifest {
        attributes([
                "Specification-Title": project.name,
                "Specification-Vendor": "mangoose",
                "Specification-Version": "1",
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                "Implementation-Vendor" : "mangoose",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task sourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
}

jar.finalizedBy('reobfJar')

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            setArtifactId("${config.mod_name}-${config.mc_version}")
            setVersion(config.mod_version)

            from components.java

            pom {
                url = "https://github.com/supermassimo/${config.mod_name}"
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://mit-license.org'
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/supermassimo/${config.mod_name}.git"
                    developerConnection = "scm:git:ssh://github.com/supermassimo/${config.mod_name}.git"
                    url = "https://github.com/supermassimo/${config.mod_name}"
                }
            }

            pom.withXml { xmlProvider ->
                final Element element = xmlProvider.asElement();

                // Clear dependencies.
                element.getChildNodes().each { node ->
                    if (node.getNodeName() == "dependencies")
                        element.removeChild(node)
                }
            }
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

idea {
    module {
        downloadSources = true
    }
}
